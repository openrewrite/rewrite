plugins {
    id 'nebula.kotlin' version '1.0.3'
    id 'me.champeau.gradle.antlr4' version '0.1'
    id 'com.gradle.plugin-publish' version '0.9.4'
    id 'java-gradle-plugin'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'nebula.plugin-plugin' version '4.24.0'
}

group = 'com.netflix.devinsight'

description 'Pluggable and distributed refactoring tool for Java source code'

contacts {
    'jkschneider@gmail.com' {
        moniker 'Jon Schneider'
        github 'jkschneider'
    }
}

repositories {
    mavenCentral()
    maven { url "http://repository.jetbrains.com/all" }
}

antlr4.output = new File('src/main/java/com/netflix/java/refactor/aspectj')
antlr4.extraArgs = ['-package', 'com.netflix.java.refactor.aspectj']

configurations {
    compile.extendsFrom antlr4
    compile.extendsFrom compileShaded
}

dependencies {
    compileShaded files("${System.getProperty('java.home')}/../lib/tools.jar")
    compile 'eu.infomas:annotation-detector:latest.release'
    compile 'org.slf4j:slf4j-api:1.7.+'
    compileOnly 'junit:junit:4.+'

    compileShaded 'org.eclipse.jgit:org.eclipse.jgit:4.4.1.201607150455-r'
    
    testCompile 'junit:junit:4.+'
    testCompile 'org.ow2.asm:asm:latest.release'
    testCompile 'org.ow2.asm:asm-util:latest.release'
    testCompile 'org.jetbrains.spek:spek:latest.release'
    testCompile 'com.github.marschall:memoryfilesystem:0.8.0'
}

pluginBundle {
    website = 'https://github.com/nebula-plugins/java-source-refactor'
    vcsUrl = 'https://github.com/nebula-plugins/java-source-refactor.git'
    description = 'Pluggable and distributed refactoring tool for Java source code'

    plugins {
        sourceRefactor {
            id = 'nebula.source-refactor'
            displayName = 'Distributed source refactoring plugin'
            description = 'Pluggable and distributed refactoring tool for Java source code'
            tags = ['nebula', 'lint']
        }
    }

    mavenCoordinates {
        groupId = 'com.netflix.devinsight'
        artifactId = 'java-source-refactor'
    }
}

tasks.withType(Javadoc) {
    // generated ANTLR sources violate doclint
    options.addStringOption('Xdoclint:none', '-quiet')
}

// Relocate jgit dependency not available in Maven Central
// Replaces the main artifact by removing the classifier for the shadow jar, and replacing jar with shadowJar
// Relocated dependencies are removed from the generated pom
shadowJar {
    configurations = [project.configurations.compileShaded]
    classifier = 'jdkbundle'

    exclude 'sun/**/*'
    exclude 'org/relaxng/**/*'
    exclude 'META-INF/services/*'
    exclude 'com/sun/xml/**/*'
    exclude 'com/sun/jarsigner/**/*'
    exclude 'com/sun/javadoc/**/*'
    exclude 'com/sun/istack/**/*'
    exclude 'com/sun/xml/**/*'
    
    relocate 'com.sun', 'com.netflix.devinsight.shaded.com.sun'
    relocate 'org.eclipse.jgit', 'com.netflix.devinsight.shaded.org.eclipse.jgit'
    relocate 'org.apache.http', 'com.netflix.devinsight.shaded.org.apache.http'
    
    mergeServiceFiles {
        exclude 'META-INF/services/com.sun.*'
    }
}

publishing {
    publications {
        nebula(MavenPublication) {
            artifact shadowJar {
                classifier = 'jdkbundle'
            }
        }

        withType(MavenPublication) {
            pom.withXml {
                asNode().dependencies.dependency.scope.each {
                    it.value = 'compile'
                }
            }
        }
    }
}
